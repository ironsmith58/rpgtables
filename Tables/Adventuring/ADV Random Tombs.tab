# ------------------------------------------------------
# Last Update: re-verified by Valminder Jan 10, 2012.
# ------------------------------------------------------
# ---------------------------------------------------
# Last Update: modified by Valminder July 12, 2006.
# ---------------------------------------------------
#
# Ancient tombs and crypts
#
# Converted from TableMaster; Copyright 1995,by Wintertree Software
# Additional changes by Larry Cormier
#
# Modified by Valminder, Dec 2005.
#   Added links to:
#      Creatures.Humanoid2
#      Miscellaneous.ClassPC
#      Miscellaneous.Gender
#      Names Generator.NAME FormalGen
#      Traps.Generate
#   Modified:
#      Renamed "Dungeon Map.tab" to "MAP Dungeon.tab"
#   Added parameters for strength of trap (Beginner's, Low, Low Average,
#                                          Average, High, Deadly, Hellish)
#   Added parameters to show the statistics of SRD traps.
#
# Modified by Valminder, July 2006.
#    Added "OverRideRolls"
#
#
# ---------------------------------------------------------------------------
# To call this table from another one, yhou can feed parameters.
# [ADV Random Tombs.Start(TrapLevel,)]
#
# TrapLevel:      4   1=Beginner's, 2=Low, 3=Low Average, 4=Average, 5=High,
#                     6=Deadly, 7=Hellish
# DisplaySRDTrap: 2   (Display the full stats of SRD Traps) 1=Yes   2=No.
# ---------------------------------------------------------------------------
#
#

/OverRideRolls 1

# Define variables
%Rooms%,0
%TempNumber%,0
%Chance%,0
%Count%,0
%DisplaySRDTrap%,2
%RoomCount%,0
%TotalCount%,0
%Denizen1%,0
%Denizen2%,0
%Denizen3%,0
%Denizen4%,0
%TrapLevel%,4

@TrapLevel,4,Choose Tomb Level Required,Beginner's,Low,Low Average,Average,High,Deadly,Hellish
@DisplaySRDTrap,2,Do you want to show the statistics of the SRD traps?,Yes,No

:Start
1,[GenerateTomb]

:Chanceof
1,|Chance={Dice~1d100}|

:GenerateTomb
1,|Rooms={Dice~1d10}|Originally, this [Form=%Rooms%] was the tomb of
_ {AorAn~[Person]}{If~%Rooms%>2?, and was protected as follows: <br><br>Room 1: [ComplexRooms]
_<br><br>Time has taken it's toll however, and although the Tomb layout itself may have changed over the years, its guardians are still here. Some may have been moved, displaced, or even disabled, and others have certainly been forgotten - but most of them are still there - just waiting to be awakened!
_<br><br><b>Here is a map of the tomb, today:<br><br>[~MAP Dungeon.GenRandomTombMap]/ [RealDetails]}</b>
_{If~%Denizen1%>0 ? <br><br>[TombMonsters=1]}
_{If~%Denizen2%>0 ? <br><br>[TombMonsters=2]}
_{If~%Denizen3%>0 ? <br><br>[TombMonsters=3]}
_{If~%Denizen4%>0 ? <br><br>[TombMonsters=4]}

:Form
1,simple barrow
2,|Rooms={Dice~1d9+1}| %Rooms%-room complex

:ComplexRooms
1,|Rooms-1||RoomCount=2|{Loop~%Rooms%,[EachRoom]<br><br>Room %RoomCount%: |RoomCount+1|}
_ Actual burial chamber[RealDetails]

:EachRoom
6,Trap: {Cap~[Trap]}.[Chanceof]{If~%Chance%<30? Secret door connects to...}
2,Two traps: {Cap~[Trap]}{If~%DisplaySRDTrap%=1?<br><br>...../,} and [Trap].[Chanceof]{If~%Chance%<30? Secret door connects to...}
4,Guarded by [Guardian].{If~%TotalCount%=0? |TotalCount=%Count%|}
1,False Tomb[RealDetails] Secret door connects to...
2,Safe Room, no traps or guardians.[Chanceof]{If~%Chance%<70? Secret door connects to...}

;Person
1,ancient king ([WhoIsThere])
1,mighty warrior ([WhoIsThere])
1,high priest ([WhoIsThere])
1,ancient hero ([WhoIsThere])
1,great villain ([WhoIsThere])

:WhoIsThere
1,[~Miscellaneous.Gender], [~Creatures.Humanoid2], [~Miscellaneous.ClassPC] named [~Names Generator.NAME FormalGen]

;Guardian
4,{If~%Denizen1%>0 ? |TempNumber={Dice~2d6}||Denizen1={Calc~(%Denizen1%+%TempNumber%)}||Count=%TempNumber%|/|Count={Dice~2d6}||Denizen1=%Count%|}
_ %Count% {If~%Count%>1 ? skeletons/ skeleton}
2,{If~%Denizen2%>0 ? |TempNumber={Dice~1d4}||Denizen2={Calc~(%Denizen2%+%TempNumber%)}||Count=%TempNumber%|/|Count={Dice~1d4}||Denizen2=%Count%|}
_ %Count% {If~%Count%>1 ? mummies/ mummy}
1,{If~%Denizen3%>1 ? |Denizen3={Calc~(%Denizen3%+1)}|/|Denizen3=1|}a ghost
3,{If~%Denizen4%>1 ? |Denizen4={Calc~(%Denizen4%+1)}|/|Denizen4=1|}a living statue
1,{If~%Denizen4%>0 ? |TempNumber={Dice~2d4}||Denizen4={Calc~(%Denizen4%+%TempNumber%)}||Count=%TempNumber%|/|Count={Dice~2d4}||Denizen4=%Count%|}
_ %Count% living statues

;RealDetails
2,, guarded by [Guardian].
2,, guarded by [Guardian]. Trap: {Cap~[Trap]}.
1,, guarded by [Guardian]. Traps: {Cap~[Trap]}{If~%DisplaySRDTrap%=1?<br><br>...../,} and [Trap].
1,. Trap: {Cap~[Trap]}.
1,. Trap: {Cap~[Trap]}{If~%DisplaySRDTrap%=1?<br><br>...../,} and [Trap].

:Trap
1,[Traps.GenerateTomb(%TrapLevel%,%DisplaySRDTrap%)]

:TombMonsters
1,<b>Skeleton</b> (%Denizen1%): Medium Undead;
_ CR: 1/3; Hit Dice: 1d12 (6 hp); Initiative: +5; Speed: 30 ft.; Armor Class:15 (+1 Dex, +2 natural, +2 heavy steel shield), touch 11, flat-footed 14;
_ BAB/Grapple: +0/+1;
_ Atks: Scimitar +1 melee (1d6+1/18-20) or 2 claws +1 melee (1d4+1); Special Attacks: -	-;
_ SQ: Damage reduction 5/bludgeoning, darkvision 60 ft., immunity to cold, undead traits; AL: NE; SV: Fort +0, Ref +1, Will +2;
_ Str 13, Dex 13, Con -, Int -, Wis 10, Cha 1;
_ Feats: Improved Initiative
2,<b>Mummy</b> (%Denizen2%): Medium Undead;
_ CR: 5; HD: 8d12+3 (55 hp); Init: +0; Spd: 20 ft.; AC: 20 (+10 natural), touch 10, flat-footed 20;
_ BAB/Grapple: +4/+11;
_ Atks: Slam +11 melee (1d6+10 plus mummy rot);
_ SA: Despair, mummy rot;
_ SQ: Damage reduction 5/-, darkvision 60 ft., undead traits, vulnerability to fire; AL: LE; SV: Fort +4, Ref +2, Will +8;
_ Str 24, Dex 10, Con -, Int 6, Wis 14, Cha 15;
_ Skills: Hide +7, Listen +8, Move Silently +7, Spot +8;
_ Feats: Alertness, Great Fortitude, Toughness
3,<b>Spectre</b> (%Denizen3%): Medium Undead (Incorporeal);
_ CR: 7; HD: 7d12 (45 hp); Init: +7; Spd: 40 ft., fly 80 ft. (perfect); AC: 15 (+3 Dex, +2 deflection), touch 15, flat-footed 13;
_ BAB/Grapple: +3/-;
_ Atks: Incorporeal touch +6 melee (1d8 plus energy drain);
_ SA: Energy drain, create spawn;
_ SQ: Darkvision 60 ft., incorporeal traits, +2 turn resistance, sunlight powerlessness, undead traits, unnatural aura; AL: LE; SV: Fort +2, Ref +5, Will +7;
_ Str -, Dex 16, Con -, Int 14, Wis 14, Cha 15;
_ Skills: Hide +13, Intimidate +12, Knowledge (religion) +12, Listen +14, Search +12, Spot +14, Survival +2 (+4 following tracks);
_ Feats: Alertness, Blind-Fight, Improved Initiative
4,<b>Stone Golem</b> (%Denizen4%): Large Construct;
_ CR: 11; Hit Dice: 14d10+30 (107 hp); Initiative: –1; Speed: 20 ft. (4 squares); Armor Class: 26 (–1 size, –1 Dex, +18 natural), touch 8, flat-footed 26;
_ Base Attack/Grapple: +10/+23;
_ Atks: 2 slams +18 melee (2d10+9); Space/Reach: 10 ft./10 ft.;
_ SA: Slow; SQ: Construct traits, damage reduction 10/adamantine, darkvision 60 ft., immunity to magic, low-light vision; AL: N
_ SV: Fort +4, Ref +3, Will +4; 
_ Str 29, Dex 9, Con —, Int —, Wis 11, Cha 1;
_ Skills: —; Feats: —;