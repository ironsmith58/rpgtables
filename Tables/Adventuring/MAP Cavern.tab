# ------------------------------------------------------
# Last Update: modified by Valminder Aug 22, 2015.
# ------------------------------------------------------
#
# Dungeon Map 2
# Copyright 2001 by Bruce A. Gulke
#
# Demonstrates the use of variables, parameters, and 
# graphics in a TableSmith file.
#
#
#
# Cavern Rev 2 by JB Willers (eventually) - March 2002
#
# After much bugging I decided to update the geomorph funCtion that Bruce
# originally started and I suped up.  The concept is quite easy to follow and
# you need a basic idea of HTML and a better than general understanding of TS
# (not that I do..)
# 
# OK to use this table copy it in the adventure or folder of your choice. 
# Copy the Cavern Tiles to a Caverns subfolder in the Graphics folder
# ie. "IMG SRC="Graphics\Caverns\"  -  Comprende?
#   
# The map is generated in a Table so it will be scollable once you export it.  
# Bruce had both a vertical and horizontal scroller once but took the
# horizontal one out for some reason.
#  
# Comments : jaybee@telkomsa.net
# Watch this space for extensions of Caverns
#
# Modified by Valminder, Nov 2005
#    Adapted for TSv5
#    Modified to use same format as Dungeon Map.tab
#    I added the subtable "GenRandomMap" to the curios.
#
# Modified by Valminder, Auf 2015
#    Added: GenRandomMapLair subtable
#

/OverrideRolls 1

%Path1%,<IMG SRC="Graphics\Caverns\
%Path2%," WIDTH="127" HEIGHT="127" BORDER="0">
%Wd%,0
%Ht%,0
%X%,0
%Y%,0

@Wd,0,Enter width:
@Ht,0,Enter height:

:Start
1,{If~%Wd%=0 ? [Instructions] / [GenMap]}

:Instructions
1,This table generates random dungeon maps. To use it, change the
_ parameters (click the parameter button) to enter the height and width
_ you want, then click the 'Roll' button.

:GenMap
1,|Y=%Ht%||Y-3|[GenTopRow][GenMiddleRows][GenBottomRow]

:GenTopRow
1,|X=%Wd%||X-3|[ULCorner][UpperEdge][URCorner]<br>

:GenMiddleRows
1,|X=%Wd%||X-3|[LeftEdge][Middle][RightEdge]<br>|Y-1|{If~%Y% >= 0 ?[GenMiddleRows]}

:GenBottomRow
1,|X=%Wd%||X-3|[LLCorner][BottomEdge][LRCorner]<br>

:ULCorner
1,%Path1%ULCorner{Dice~1d3}.jpg%Path2%

:UpperEdge
1,%Path1%UEdge{Dice~1d3}.jpg%Path2%|X-1|{If~%X% >= 0 ?[UpperEdge]}

:URCorner
1,%Path1%URCorner{Dice~1d3}.jpg%Path2%

:LLCorner
1,%Path1%LLCorner{Dice~1d3}.jpg%Path2%

:BottomEdge
1,%Path1%BEdge{Dice~1d3}.jpg%Path2%|X-1|{If~%X% >= 0?[BottomEdge]}

:LRCorner
1,%Path1%LRCorner{Dice~1d3}.jpg%Path2%

:LeftEdge
1,%Path1%LEdge{Dice~1d3}.jpg%Path2%

:Middle
1,%Path1%Middle{Dice~1d3}.jpg%Path2%|X-1|{If~%X% >= 0?[Middle]}

:RightEdge
1,%Path1%REdge{Dice~1d3}.jpg%Path2%

# ------------------------------------------------------------------------
# I added this to generate a random map found in a treasure.
#

:GenRandomMap
1,|Wd={Dice~2d2+1}||Ht={Dice~2d2+1}|[GenMap]

:GenRandomMapLair
1,|Wd={Dice~1d3+1}||Ht={Dice~1d3+1}|[GenMap]

